apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.chenenyu.security"
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile file("../keystore.jks")
            storePassword "123456"
            keyAlias "key"
            keyPassword "123456"
        }
    }
    buildTypes {
        debug {
            matchingFallbacks = ['release', 'debug']
            signingConfig signingConfigs.debug
        }
        release {
            matchingFallbacks = ['release', 'debug']
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        prod_debug.initWith(buildTypes.debug)
        sit_debug.initWith(buildTypes.debug)
        dev_debug.initWith(buildTypes.debug)

        prod_release.initWith(buildTypes.release)
        sit_release.initWith(buildTypes.release)
        dev_release.initWith(buildTypes.release)

        prod_debug {}
        prod_release {}

        sit_debug {
            getIsDefault().set(true)
        }
        sit_release {}

        dev_debug {}
        dev_release {}
    }

    flavorDimensions "normal"

    productFlavors {
        bibigo {
            dimension "normal"
        }
        global {
            dimension "normal"
        }
    }

    variantFilter { variant ->
        def buildTypename = variant.buildType.name.toString()
        if (buildTypename.equals('release') || buildTypename.equals('debug')) {
            variant.setIgnore(true)
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation project(path: ':signature_checker')
}
